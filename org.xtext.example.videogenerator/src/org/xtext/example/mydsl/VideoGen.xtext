grammar org.xtext.example.mydsl.VideoGen with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate videoGen "http://www.xtext.org/example/mydsl/VideoGen"

VideoGeneratorModel: {VideoGeneratorModel}
	(information=VideoGenInformation)?
	'VideoGen' LEFT_BRACKET
	videoseqs+=VideoSeq+
	RIGHT_BRACKET
	;

VideoGenInformation: {VideoGenInformation}
('@author' authorName=STRING)
('@version' version=STRING)?
('@creation' creationDate=STRING)?	
;
	
VideoSeq: MandatoryVideoSeq | OptionalVideoSeq | AlternativeVideoSeq ; 
	
MandatoryVideoSeq : 'mandatory' description=VideoDescription;
OptionalVideoSeq : 'optional' description=VideoDescription;
AlternativeVideoSeq : 'alternatives' (videoid=ID)? LEFT_BRACKET videodescs+=VideoDescription+ RIGHT_BRACKET;

VideoDescription : 'videoseq' (videoid=ID)? location=STRING
	(LEFT_BRACKET
	 ('duration' duration=FLOAT)?
	 ('probability' probability=PERCENTAGE)?
	 ('size' size=INT)?
	 ('description' description=STRING)? 
	RIGHT_BRACKET)?
;

terminal PERCENTAGE returns ecore::EInt: ('1'..'9')('0'..'9')? | '100';
terminal FLOAT returns ecore::EFloat: INT'.'INT;
terminal LEFT_BRACKET: '{' ; 
terminal RIGHT_BRACKET: '}';